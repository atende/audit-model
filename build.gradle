apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "info.atende.audition"
archivesBaseName = "audit-model"
version = "1.0.0-SNAPSHOT"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
ext.ossrhUsername = 'build'
ext.ossrhPassword = 'test'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'
    testCompile 'junit:junit:4.10'
}



uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Audit Model'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Model classes for Audit Project.'
                url 'https://atende.github.io/audit-model/'

                scm {
                    connection 'scm:git:git@github.com:atende/audit-model.git'
                    developerConnection 'scm:git:git@github.com:atende/audit-model.git'
                    url 'https://github.com/atende/audit-model'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'giovanni'
                        name 'Giovanni Silva'
                        email 'giovanni@atende.info'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.13'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


